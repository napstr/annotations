import com.github.spotbugs.SpotBugsTask
import org.gradle.api.tasks.wrapper.Wrapper.DistributionType

plugins {
    id 'com.github.spotbugs' version '1.6.1'
}

group 'space.npstr.annotations'
version '0.0.2'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.github.spotbugs'

sourceCompatibility = 8
targetCompatibility = 8

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId rootProject.group
            artifactId project.name

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

repositories {
    repositories {
        mavenCentral()                          // everything else
        mavenLocal()                            // local maven repo, mostly for testing stuff
        maven { url 'https://jitpack.io' }      // for getting builds from github
    }
}

dependencies {
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '3.1.2' //annotations
}

compileJava.dependsOn clean
compileJava.options.encoding = 'UTF-8'
compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"

jar.mustRunAfter clean
jar.dependsOn spotbugsMain

publishToMavenLocal.dependsOn jar

task install {
    dependsOn publishToMavenLocal
    doLast {
        println "Version: " + version
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5.1'
    distributionType = DistributionType.ALL
}

// To generate an HTML report instead of XML
tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
